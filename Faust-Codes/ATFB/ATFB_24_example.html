<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8" />
  <script type="module" src="faust-web-component.js"></script>

  <style>
    body {
      margin: 0;
      padding: 0;
    }
    #editor {
      border: 1px solid black;
      --faust-font-size: 12px;
      --faust-line-height: 1.25;
    }
  </style>
</head>
<body>

<faust-editor id="editor">
<!--
import("stdfaust.lib");

// Robert Bristow-Johnson's Biquad Filter - Direct Form 1
// https://webaudio.github.io/Audio-EQ-Cookbook/audio-eq-cookbook.html
biquad(i, cf, q) = _ : coefficients(i) : biquadFilter
     with{
         biquadFilter(a0, a1, a2, b1, b2) = biquadFilter
             with{
                 biquadFilter =  _ <: _, (mem  <: (_, mem)) : (_ * a0, _ * a1, _ * a2) :> _ :  
                                 ((_, _) :> _) ~ (_ <: (_, mem) : (_ * -b1, _ * -b2) :> _);
             };

         // Angular Frequency formula
         omega(x) = (2 * ma.PI * x) / ma.SR;
         // Angular Frequency in the sine domain
         sn(x) = sin(omega(x));
         // Angular Frequency in the cosine domain
         cs(x) = cos(omega(x));  
         // Alpha
         alpha(cf0, q0) = sin(omega(cf0)) / (2 * q0);

         // Robert Bristow-Johnson's Biquad Filter - Coefficents
         // Lowpass Filter
         coefficients(0) = a0, a1, a2, b1, b2, _
         with{
             b0 = (1 + alpha(cf, q));
             a0 = ((1 - cs(cf)) / 2) / b0;
             a1 = (1 - cs(cf)) / b0;
             a2 = ((1 - cs(cf)) / 2) / b0;
             b1 = (-2 * cs(cf)) / b0;
             b2 = (1 - alpha(cf, q)) / b0;
         };
         // Highpass filter
         coefficients(1) = a0, a1, a2, b1, b2, _
         with{
             b0 = (1 + alpha(cf, q));
             a0 = ((1 + cs(cf)) / 2) / b0;
             a1 = (-1 * (1 + cs(cf))) / b0;
             a2 = ((1 + cs(cf)) / 2) / b0;
             b1 = (-2 * cs(cf)) / b0;
             b2 = (1 - alpha(cf, q)) / b0;
         };
         // Bandpass Filter
         coefficients(2) = a0, a1, a2, b1, b2, _
         with{
             b0 = 1 + alpha(cf, q);
             a0 = alpha(cf, q) / b0;
             a1 = 0;
             a2 = - alpha(cf, q) / b0;
             b1 = (-2 * cs(cf)) / b0;
             b2 = (1 - alpha(cf, q)) / b0;
         };
         // Notch filter
         coefficients(3) = a0, a1, a2, b1, b2, _
         with{
             b0 = 1 + alpha(cf, q);
             a0 = 1 / b0;
             a1 = (-2 * cs(cf)) / b0;
             a2 = 1 / b0;
             b1 = (-2 * cs(cf)) / b0;
             b2 = (1 - alpha(cf, q)) / b0;
         };
         // Peaking EQ filter
         coefficients(4) = a0, a1, a2, b1, b2, _
         with{
             A = 10;
             b0 = 1 + (alpha(cf, q) / A);
             a0 = (1 + (alpha(cf, q) * A)) / b0;
             a1 = (-2 * cs(cf)) / b0;
             a2 = (1 - (alpha(cf, q) * A)) / b0;
             b1 = (-2 * cs(cf)) / b0;
             b2 = (1 - (alpha(cf, q) / A)) / b0;
         };
         // Low Shelf Filter
         coefficients(5) = a0, a1, a2, b1, b2, _
         with{
             //dbGain 20;
             A  = pow(10, -20 /40);
             beta = sqrt(A + A);
             b0 = (A + 1) + (A - 1) * cs(cf) + beta * alpha(cf, q);
             a0 = (A * ((A + 1) - (A - 1) * cs(cf) + beta * alpha(cf, q))) /b0;
             a1 = (2 * A * ((A - 1) - (A + 1) * cs(cf))) / b0;
             a2 = (A * ((A + 1) - (A - 1) * cs(cf) - beta * alpha(cf, q))) /b0;
             b1 = (-2 * ((A - 1) + (A + 1) * cs(cf))) / b0;
             b2 = ((A + 1) + (A - 1) * cs(cf) - beta * alpha(cf, q)) / b0;
         };
         // High Shelf Filter
         coefficients(6) = a0, a1, a2, b1, b2, _
         with{
             //dbGain 20;
             A  = pow(10, -20 /40);
             beta = sqrt(A + A);
             b0 = (A + 1) - (A - 1) * cs(cf) + beta * alpha(cf, q);
             a0 = (A * ((A + 1) + (A - 1) * cs(cf) + beta * alpha(cf, q))) /b0;
             a1 = (2 * A * ((A - 1) + (A + 1) * cs(cf))) / b0;
             a2 = (A * ((A + 1) + (A - 1) * cs(cf) - beta * alpha(cf, q))) /b0;
             b1 = (2 * ((A - 1) - (A + 1) * cs(cf))) / b0;
             b2 = ((A + 1) - (A - 1) * cs(cf) - beta * alpha(cf, q)) / b0;
         };
};
process = (1000, 1, _) : biquad(0);
-->
</faust-editor>

<script>
// Aspetta che il web component sia pronto
customElements.whenDefined("faust-editor").then(() => {
  const editor = document.getElementById("editor");

  // 1) Cambia il tema in "light" totalmente neutro
  editor.setAttribute("theme", "light");

  // 2) Elimina la syntax colorata dentro CodeMirror
  const shadow = editor.shadowRoot;
  const style = document.createElement("style");

  style.textContent = `
    .cm-content, .cm-line, .cm-scroller {
      color: black !important;
      background: white !important;
      font-size: 12px !important;
      line-height: 1.25 !important;
    }
    .cm-content span {
      color: black !important;
      font-weight: normal !important;
    }
  `;

  shadow.appendChild(style);
});
</script>

</body>
</html>